
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.Проводки.БлокироватьДляИзменения = Истина;
	Движения.Проводки.Записывать = Истина;
	Движения.Проводки.Записать();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Начало", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Окончание", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетПС", ПланыСчетов.Бухгалтерский.НайтиПоКоду("1.6.1"));
	Запрос.УстановитьПараметр("СчетАМ", ПланыСчетов.Бухгалтерский.НайтиПоКоду("1.6.2"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СвойстваОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СвойстваОС.Состояние КАК Состояние,
	|	СвойстваОС.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ЕСТЬNULL(ОстаткиПС.СуммаОстаток, 0) КАК СуммаПС,
	|	ЕСТЬNULL(ОстаткиАМ.СуммаОстаток, 0) КАК СуммаАМ,
	|	ВЫРАЗИТЬ(ОстаткиПС.СуммаОстаток / СвойстваОС.СрокПолезногоИспользования КАК ЧИСЛО(10, 2)) КАК СуммаАмортизации,
	|	ЕСТЬNULL(ОборотыАМ.СуммаОборотКт, 0) КАК СуммаОборотКт,
	|	ОстаткиПС.Подразделение,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(СвойстваОсновныхСредствСрезПервых.Период, МЕСЯЦ), &Начало, МЕСЯЦ) КАК МесяцевВведено
	|ИЗ
	|	РегистрСведений.СвойстваОсновныхСредств.СрезПоследних(&Период, Организация = &Организация) КАК СвойстваОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Проводки.Остатки(&Период, Счет = &СчетПС, , Организация = &Организация) КАК ОстаткиПС
	|		ПО СвойстваОС.ОсновноеСредство = ОстаткиПС.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Проводки.Остатки(&Период, Счет = &СчетАМ, , Организация = &Организация) КАК ОстаткиАМ
	|		ПО СвойстваОС.ОсновноеСредство = ОстаткиАМ.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Проводки.Обороты(&Начало, &Окончание, Месяц, Счет = &СчетАМ, , Организация = &Организация, , ) КАК ОборотыАМ
	|		ПО СвойстваОС.ОсновноеСредство = ОборотыАМ.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваОсновныхСредств.СрезПервых(
	|				,
	|				Организация = &Организация
	|					И Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОсновногоСредства.Используется)) КАК СвойстваОсновныхСредствСрезПервых
	|		ПО СвойстваОС.ОсновноеСредство = СвойстваОсновныхСредствСрезПервых.ОсновноеСредство
	|ГДЕ
	|	СвойстваОС.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОсновногоСредства.Используется)";
	Результат = Запрос.Выполнить();	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаОборотКт < Выборка.СуммаАмортизации 
			И Выборка.СуммаПС + Выборка.СуммаАМ > 0 
			И Выборка.МесяцевВведено > 0 Тогда
			Движение = Движения.Проводки.Добавить();
			Движение.СчетДт = ПланыСчетов.Бухгалтерский.Капитал;
			Движение.СчетКт = ПланыСчетов.Бухгалтерский.НайтиПоКоду("1.6.2");
			БухгалтерияСервер.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, 1, Выборка.ОсновноеСредство);
			Движение.Период = КонецМесяца(Дата);
			Движение.Организация = Организация;
			Движение.ПодразделениеКт = Выборка.Подразделение;
			Движение.Сумма = Выборка.СуммаАмортизации - Выборка.СуммаОборотКт;
			Движение.Содержание = "Начислена амортизация";
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры
